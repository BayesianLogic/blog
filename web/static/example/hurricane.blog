/**
 * Hurricane 
 * Figure 4.2 in Milch's thesis
 */
 
type City;
type PrepLevel;
type DamageLevel;

random City First ~ UniformChoice({c for City c});

random City NotFirst ~ UniformChoice({c for City c: c != First});

random PrepLevel Prep(City c) ~
  if (First == c) then Categorical({High -> 0.5, Low -> 0.5})
  else case Damage(First) in 
            {Severe -> Categorical({High -> 0.9, Low -> 0.1}),
                    Mild -> Categorical({High -> 0.1, Low -> 0.9})}
            ;

random DamageLevel Damage(City c) ~
   case Prep(c) in {High -> Categorical({Severe -> 0.2, Mild -> 0.8}),
            Low -> Categorical({Severe -> 0.8, Mild -> 0.2})}
          ;

distinct City A, B;
distinct PrepLevel Low, High;
distinct DamageLevel Severe, Mild;

obs Damage(First) = Severe;

query First;
query Damage(A);
query Damage(B);
query Damage(NotFirst);
