/**
 * background mixture models
 * This BLOG program describe the model for image background 
 * segmentation. 
 * See the paper:
 *   Image segmentation in video sequences: A probabilistic approach
 *   Nir Friedman, Stuart Russell, UAI 1997
 * In particular, each pixel is drawn from a mixture of road, shadow
 * vehicle components. Each component is modeled as a Gaussian 
 * distribution. 
 *
 * This version doesnot utilize internal notion of pixel.
 * This version assumes a fixed set of parameters for each components.
 * However, we use the variable to store the Mean of each component
 * which can be set by user.
 * author: leili
 */

type Component;

distinct Component Road, Shadow, Vehicle;

random Component AssociatedComponent(Integer x, Integer y, Timestep t) 
  ~ Categorical({Road -> 0.7, 
                Shadow -> 0.1,
                Vehicle -> 0.2});

/*Make a 10x10 grid, over 10 timesteps*/

random Real[] Intensity(Integer x, Integer y, Timestep t) {
    if AssociatedComponent(x, y, t) == Vehicle then 
        ~ MultivarGaussian([240; 240; 240], [10, 0, 0;
	  			      	     0, 10, 0;
					     0, 0, 10])
    else if AssociatedComponent(x, y, t) == Shadow then 
    	~ MultivarGaussian([10; 10; 10], [10, 0, 0;
	  			     	  0, 10, 0;
					  0, 0, 10])
    else if AssociatedComponent(x, y, t) == Road then 
    	~ MultivarGaussian([100; 100; 100], [10, 0, 0;
	  			      	     0, 10, 0;
					     0, 0, 10])
};

obs Intensity(1, 1, @1) = [135; 85; 170];

query AssociatedComponent(1, 1, @1);