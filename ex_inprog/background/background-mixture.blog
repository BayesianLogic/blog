/**
 * background mixture models
 * This BLOG program describe the model for image background 
 * segmentation. 
 * See the paper:
 *   Image segmentation in video sequences: A probabilistic approach
 *   Nir Friedman, Stuart Russell, UAI 1997
 * In particular, each pixel is drawn from a mixture of road, shadow
 * vehicle components. Each component is modeled as a Gaussian 
 * distribution. 
 *
 * This version assumes a fixed set of parameters for each components.
 * All the parameters are predefined. 
 * author: leili
 */

type Component;
type Pixel;

guaranteed Component Road, Shadow, Vehicle;
origin Timestep Time(Pixel);
origin NaturalNum X(Pixel);
origin NaturalNum Y(Pixel);
random R3Vector Intensity(Pixel);
random Component AssociatedComponent(Pixel);

#Pixel(X=x, Y=y, Time=t)=1;

AssociatedComponent(pixel) ~ TabularCPD[[0.7, 0.1, 0.2]]();

Intensity(pixel) {
    if AssociatedComponent(pixel) = Road then 
        ~ MultivarGaussian(3, [240, 240, 240], 
                              [10, 0, 0; 
                                0, 10, 0; 
                                0, 0, 10])
    elseif AssociatedComponent(pixel) = Shadow then
        ~ MultivarGaussian(3, [10, 10, 10], 
                              [10, 0, 0; 
                                0, 10, 0; 
                                0, 0, 10])
    elseif AssociatedComponent(pixel) = Vehicle then
        ~ MultivarGaussian(3, [100, 100, 100], 
                              [10, 0, 0; 
                                0, 10, 0; 
                                0, 0, 10])
}

