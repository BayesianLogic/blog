/**
 * A simple DBLOG model with a single atemporal variable.
 */

type RainEvent;
distinct RainEvent Rainy, Dry;

random Boolean RainyRegion ~ Bernoulli(0.5);

random RainEvent Weather(Timestep d) {
    if (d == @0) then ~ TabularCPD({true -> ~ Categorical({Rainy -> 0.7, Dry -> 0.3}),
       	    	       		    false -> ~ Categorical({Rainy -> 0.3, Dry -> 0.7})}, RainyRegion)
    else {
    	if RainyRegion then ~ TabularCPD({Rainy -> ~ Categorical({Rainy -> 0.8, Dry -> 0.2}),
    	    		       		  Dry -> ~ Categorical({Rainy -> 0.3, Dry -> 0.7})}, Weather(Prev(d)))
        else ~ TabularCPD({Rainy -> ~ Categorical({Rainy -> 0.5, Dry -> 0.5}),
       		           Dry -> ~ Categorical({Rainy -> 0.2, Dry -> 0.8})}, Weather(Prev(d)))
    }
};

obs Weather(@0) = Rainy;
obs Weather(@1) = Rainy;
obs Weather(@2) = Rainy;
obs Weather(@3) = Rainy;
obs Weather(@4) = Dry;
obs Weather(@5) = Dry;

query Weather(@6);
