/**
 * Tug of war
 * 
 * The strength of each person is drawn from a normal distribution. 
 * A person is lazy 10% of the time. The pulling power of a person 
 * is half her strength if she is being lazy and otherwise is her 
 * full strength. The pulling power of a team is the sum of the 
 * pulling power of the individuals on the team. The team with the 
 * greater pulling power wins.
 * 
 * This version uses lambda expression and reduce function.
 * 
 * @author Lei Li
 * @date 2013/03/30
 */

type Person;
distinct Person James, David, Christopher, 
  George, Ronald, John, Richard, Daniel, 
  Kenneth, Anthony, Robert, Charles, Paul, 
  Steven, Kevin, Michael, Joseph, Mark, Edward, 
  Jason, William, Thomas, Donald, Brian, Jeff;
distinct Person Mary, Jennifer, Lisa, Sandra, Michelle,
  Patricia, Maria, Nancy, Donna, Laura, Linda, Susan,
  Karen, Carol, Sarah, Barbara, Margaret, Betty, Ruth,
  Kimberly, Elizabeth, Dorothy, Helen, Sharon, Deborah;
fixed Real mu = 10;
fixed Real sigma = 2;
random Real strength(Person p) ~ Gaussian(mu, sigma);

random Person[] team1(Integer i) // 1st team of i-th match 
  = ~ UniformChoice({Person p}, 3);

random Person[] team2(Integer i) // 2nd team of i-th match 
  = ~ UniformChoice({Person p}, 3);

random Boolean lazy(Person p, Integer i) 
  ~ Bernoulli(0.1); // person p at i-th match

random Real pulling_power(Person p, Integer i) 
	if lazy(p, i) then = strength(p) / 2
	else = strength(p);

random Real team_power(Person[] ps, i)
  = reduce(__PLUS, map(lambda x : pulling_power(x, i), ps));

random Boolean team1win(Integer i)
	if (team_power(team1(i), i) > team_power(team2(i), i)) 
	then = true
	else = false;

obs team1(1) = [James, David, Christopher];
obs team2(1) = [George, Ronald, John];

obs team1(2) = [James, David, Anthony];
obs team2(2) = [Christopher, Joseph, John];

obs team1(3) = [Daniel, Jeff, Christopher];
obs team2(3) = [George, David, James];

obs team1win(1) = true;

query team1win(2);
query team1win(3);
query strength(James);
