/* The class burglary-earthquake network, as presented in "Artificial 
 * Intelligence: A Modern Approach", 2nd ed., p. 494.  All the CPDs are 
 * tabular.  We take advantage of the option of leaving out the last 
 * element of each row in the parameters to TabularCPD.  
 */

random Boolean Burglary ~ BooleanDistrib(0.001);

random Boolean Earthquake ~ BooleanDistrib(0.002);

random Boolean Alarm ~ TabularCPD({[true, true] -> ~ BooleanDistrib(0.95),
       	   [true, false] -> ~ BooleanDistrib(0.94),
				   [false, true] -> ~ BooleanDistrib(0.29),
				   [false, false] -> ~ BooleanDistrib(0.001)},
			     [Burglary, Earthquake]);

random Boolean JohnCalls ~ TabularCPD({true -> ~ BooleanDistrib(0.9),
       	       		   	       false -> ~ BooleanDistrib(0.05)},
				 Alarm);

random Boolean MaryCalls ~ TabularCPD({true -> ~ BooleanDistrib(0.7),
       	       		   	       false -> ~ BooleanDistrib(0.01)},
				 Alarm);

/* Evidence for the burglary model (burglary.mblog) saying that both 
 * John and Mary called.  Given this evidence, the posterior probability 
 * of Burglary is 0.284 (see p. 505 of "AI: A Modern Approach", 2nd ed.).
 */

obs JohnCalls = true;
obs MaryCalls = true;

/* Query for the burglary model (burglary.mblog) asking whether Burglary 
 * is true.
 */

query Burglary;