/* scene-and-or-2.blog
 * -------------------
 * Represents an and-or tree for representation of scenes
 * @author dheeraj
 * @date 2013-01-02
 */

type World;
type Scene;
type Cow;
type Person;
type Bike;
type Cart;
type PersonPose;
type Wheel;

origin World ParentWorld(Scene);
origin Scene ParentScene(Cow);
origin Scene ParentScene(Person);
origin Scene ParentScene(Cart);
origin Scene ParentScene(Bike);
origin Person ParentPerson(PersonPose);
origin Bike ParentBike(Wheel);
origin Cart ParentCart(Wheel);


#Wheel(ParentBike = b) {
    if b != null then
        = 3
    else 
        = 0
};

#Wheel(ParentCart = pc) {
    if pc != null then
        = 2
    else 
        = 0
};


distinct World theWorld;

#Scene(ParentWorld = w){
    if w!=null then
        =1
    else
        =0
};

random Integer WhichScene(World w) {
    if w != null then
        ~ UniformInt(1, 2)
    else
        = 0
};

random Boolean IsStreet(Scene s) {
    if (s != null & ParentWorld(s) != null & WhichScene(ParentWorld(s)) == 1) then
        = true
    else
        = false
};

random Boolean IsFarm(Scene s) {
    if (s != null & ParentWorld(s) != null & WhichScene(ParentWorld(s)) == 2) then
        = true
    else
        = false
};

#Person(ParentScene = s) {
    if (s != null & IsStreet(s)) then
        ~ UniformInt(5, 10)
    else if (s != null & IsFarm(s)) then
        ~ UniformInt(1, 3)
    else
        = 0
};

#PersonPose(ParentPerson = p) {
    if (p != null) then
        = 1
    else 
        = 0
};

random Integer WhichPose(Person p) {
    if (p != null) then
        ~ UniformInt(1, 2)
    else
        = 0
};

random Boolean IsFrontal(PersonPose p) {
    if (p != null & ParentPerson(p) != null & WhichPose(ParentPerson(p)) == 1) then
        = true
    else
        = false
};

random Boolean IsSideway(PersonPose p) {
    if (p != null & ParentPerson(p) != null & WhichPose(ParentPerson(p)) == 2) then
        = true
    else
        = false
};

#Bike(ParentScene = s) {
    if (s != null & IsStreet(s)) then
        ~ UniformInt(0, 3)
    else
        = 0
};

#Cow(ParentScene = s) {
    if (s != null & IsFarm(s)) then
        ~ UniformInt(1, 5)
    else
        = 0
};

#Cart(ParentScene = s) {
    if(s != null & IsFarm(s)) then
        ~ UniformInt(0, 3)
    else
        = 0
};


//obs {Wheel w}  = {W1, W2};
//obs {Wheel w}  = {W1, W2, W3};
obs {Wheel w}  = {W1, W2, W3, W4, W5, W6};
//obs {Wheel w}  = {W1, W2, W3, W4, W5};

/* Query file asking for the total number of balls in the urn.
 */

//query #{World w};
//query #{Scene s};
query #{Scene s: IsStreet(s) == true};
query #{Scene s: IsFarm(s) == true};
query #{Cow c};
query #{Person p};
//query #{PersonPose p};
//query #{PersonPose p: IsFrontal(p) = true};
//query #{PersonPose p: IsSideway(p) = true};
query #{Bike b};
query #{Cart c};
query #{Wheel w};
