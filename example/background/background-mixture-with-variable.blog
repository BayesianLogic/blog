/**
 * background mixture models
 * This BLOG program describe the model for image background 
 * segmentation. 
 * See the paper:
 *   Image segmentation in video sequences: A probabilistic approach
 *   Nir Friedman, Stuart Russell, UAI 1997
 * In particular, each x/y/time's associated component
 * is drawn from a mixture of road, shadow vehicle components. Each
 * component is modeled as a Gaussian distribution. 
 *
 * This version assumes a fixed set of parameters for each components.
 * However, we use the variable to store the Mean of each component
 * which can be set by user
 * author: leili
 */

type Component;

distinct Component Road, Shadow, Vehicle;

fixed Real[] mean_car = [240; 240; 240];
fixed Real[] mean_shadow = [10; 10; 10];
fixed Real[] mean_road = [100; 100; 100];

fixed Real[][] variance = [10000, 0, 0;
      	       		   0, 10000, 0;
			   0, 0, 10000];

fixed Real[] Mean(Component c)
    = TabularInterp(1,
	Vehicle, mean_car,
	Shadow, mean_shadow,
	Road, mean_road);

random Component AssociatedComponent(Integer x, Integer y, Timestep t)
    ~ Categorical({Road -> 0.7, 
                   Shadow -> 0.1,
    		   Vehicle -> 0.2});

// the parameters can be set in Mean(Component)
random Real[] Intensity(Integer x, Integer y, Timestep t)
     ~ MultivarGaussian(Mean(AssociatedComponent(x, y, t)), variance);

obs Intensity(1, 1, @1) = [135; 85; 170];

query AssociatedComponent(1, 1, @1);