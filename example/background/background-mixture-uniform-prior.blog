/**
 * background mixture models
 * This BLOG program describe the model for image background 
 * segmentation. 
 * See the paper:
 *   Image segmentation in video sequences: A probabilistic approach
 *   Nir Friedman, Stuart Russell, UAI 1997
 * In particular, each x/y/time's associated component
 * is drawn from a mixture of road, shadow vehicle components. Each
 * component is modeled as a Gaussian distribution. 
 *
 * This version assumes a prior distribution over distribution of 
 * parameters for each components.
 *
 * author: leili
 */

type Component;

distinct Component Road, Shadow, Vehicle;

fixed RealMatrix variance = [10000, 0, 0;
                             0, 10000, 0;
                             0, 0, 10000];

fixed RealMatrix rangeR = [0.0, 255.0];
fixed RealMatrix rangeG = [0.0, 255.0];
fixed RealMatrix rangeB = [0.0, 255.0];
random RealMatrix Mean(Component c)
    ~ UniformVector(rangeR, rangeG, rangeB);

fixed RealMatrix WeightParams = [0.33, 0.33, 0.33];
random RealMatrix Weight ~ Dirichlet(WeightParams);

random Integer AssociatedComponent(Integer x, Integer y, Timestep t)
    ~ Discrete(Weight);

fixed Component IntToComponent(Integer c)
    = TabularInterp(1, 0, Road,
                 1, Shadow,
           2, Vehicle);

// the parameters can be set in Mean(Component)
random RealMatrix Intensity(Integer x, Integer y, Timestep t)
    ~ MultivarGaussian(
        transpose(Mean(IntToComponent(AssociatedComponent(x, y, t)))),
        variance);

obs Intensity(1, 1, @1) = [135, 85, 170];

query IntToComponent(AssociatedComponent(1, 1, @1));
