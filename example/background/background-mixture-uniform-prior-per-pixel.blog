/**
 * background mixture models
 * This BLOG program describe the model for image background 
 * segmentation. 
 * See the paper:
 *   Image segmentation in video sequences: A probabilistic approach
 *   Nir Friedman, Stuart Russell, UAI 1997
 * In particular, each x/y/timestep's associated component is drawn from
 * a mixture of road, shadow, and vehicle components. Each component is
 * modeled as a Gaussian distribution. 
 *
 * This version assumes a prior distribution over distribution of 
 * parameters for each component. And the parameters of each component 
 * is on per position basis. The weight is also on per position basis.
 *
 * author: leili
 */

type Component;

distinct Component Road, Shadow, Vehicle;

fixed Real[][] variance = [10000, 0, 0;
      	       		   0, 10000, 0;
			   0, 0, 10000];

// the prior posts uniform distribution over a box of 3D space.
random Real[] Mean(Component c, Integer x, Integer y)
    ~ UniformVector(0, 255, 0, 255, 0, 255);

random Real[] Weight(Integer x, Integer y)
    ~ Dirichlet(0.33, 0.33, 0.33);

random Integer AssociatedComponent(Integer x, Integer y, Timestep t)
    ~ Categorical(Weight(x, y));

fixed Component IntToComponent(Integer c)
    = TabularInterp(1, 0, Road,
      		       1, Shadow,
		       2, Vehicle);

// the parameters can be set in Mean(Component)
random Real[] Intensity(Integer x, Integer y, Timestep t)
    ~ MultivarGaussian(Mean(IntToComponent(AssociatedComponent(x, y, t)), x, y), variance);

obs Intensity(1, 1, @1) = [135; 85; 170];

query IntToComponent(AssociatedComponent(1, 1, @1));