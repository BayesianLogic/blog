/**
 * Hurricane 
 * Figure 4.2 in Milch's thesis
 */
 
type City;
type PrepLevel;
type DamageLevel;

random City First ~ UniformChoice({City c});

random City NotFirst ~ UniformChoice({City c: c != First});

random PrepLevel Prep(City c) {
       if (First == c) then ~ Categorical({High -> 0.5, Low -> 0.5})
       else ~ TabularCPD({Severe -> ~ Categorical({High -> 0.9, Low -> 0.1}),
       	      		  Mild -> ~ Categorical({High -> 0.1, Low -> 0.9})},
		    Damage(First))
};

random DamageLevel Damage(City c) {
	~ TabularCPD({High -> ~ Categorical({Severe -> 0.2, Mild -> 0.8}),
	  	      Low -> ~ Categorical({Severe -> 0.8, Mild -> 0.2})},
		Prep(c))
};

distinct City A, B;
distinct PrepLevel Low, High;
distinct DamageLevel Severe, Mild;

obs First = A;
obs Damage(First) = Severe;

query Damage(NotFirst);
query NotFirst;
