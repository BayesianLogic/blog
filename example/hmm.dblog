/* A hidden Markov model with four states and four output symbols.
 * The parameters of this model are completely made-up.
 * DBLOG model
 */

type State;
guaranteed State A, C, G, T;

type Output;
guaranteed Output ResultA, ResultC, ResultG, ResultT;

random State S(Timestep);
random Output O(Timestep);

S(t) {
    if t = @0 then ~ TabularCPD[[0.3, 0.2, 0.1, 0.4]]()
    else ~ TabularCPD[[0.1, 0.3, 0.3, 0.3], 
                      [0.3, 0.1, 0.3, 0.3],
                      [0.3, 0.3, 0.1, 0.3],
                      [0.3, 0.3, 0.3, 0.1]](S(Prev(t)))
};

O(t) {
    ~ TabularCPD[[0.85, 0.05, 0.05, 0.05],
                 [0.05, 0.85, 0.05, 0.05],
                 [0.05, 0.05, 0.85, 0.05],
                 [0.05, 0.05, 0.05, 0.85]](S(t))
};

/* Evidence for the Hidden Markov Model.
 */

obs O(@0) = ResultC;
obs O(@1) = ResultA;
obs O(@2) = ResultA;
obs O(@3) = ResultA;
obs O(@4) = ResultG;

/* Queries for the Hiddem Markov Model, given the evidence.  
 * Note that we can query S(5) even though our observations only 
 * went up to time 4.
 */

query S(@0);
query S(@1);
query S(@2);
query S(@3);
query S(@4);
query S(@5);
