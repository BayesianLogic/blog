type Aircraft;
type Blip;

random Real Location(Aircraft);
origin Aircraft Source(Blip);
random Real BlipLocation(Blip);

#Aircraft ~ Poisson(3.0);

random Real Location(a) ~ UniformReal(100.0, 200.0);

#Blip ~ Poisson(2.0);

#Blip(Source = a) ~ Poisson(1.0);

BlipLocation(b) {
        if Source(b) != null 
	then ~ UnivarGaussian(Location(Source(b)), 1.0);
        else ~ UniformReal(90.0, 210.0);
};

obs {Blip b} = {b1, b2, b3, b4};

obs BlipLocation(b1) = 156;
obs BlipLocation(b2) = 133;
obs BlipLocation(b3) = 158;
obs BlipLocation(b4) = 180;

query #{Aircraft a};
query Location(Source(b1));
query Location(Source(b2));
query Location(Source(b3));
query Location(Source(b4));

/* TESTBLOG: -n 100000 -b 10000
blogc *
Samples done: 100000. Elapsed *
 Query1: P(null) ~0 Avg. ~2.2 S.D. *
 Query2: P(null) ~.19 Avg. ~157 S.D. ~.87
 Query3: P(null) ~.62 Avg. ~133 S.D. ~1
 Query4: P(null) ~.19 Avg. ~157 S.D. ~.87
 Query5: P(null) ~.62 Avg. ~180 S.D. ~1
*/
