/**
 * Sybil attack
 * Typically between 100 and 10,000 real people, about 90% are honest 
 * who has one login ID. Dishonest people can have multiple logins 
 * each. Transactions may occur between logins. If two logins are 
 * owned by the same person, then a transaction is highly likely; 
 * Otherwise, transaction is less likely (depending on honesty 
 * of each login's owner). A login may recommend another after 
 * a transaction: Logins with the same owner usually recommend 
 * each other; Otherwise, probability of recommendation depends 
 * on the honesty of the two owners.
 *
 * @author: leili
 * @date: 2011-12-21
 * @date: 2012-09-13
 */

type Person;
type Login;

//#Person ~ Poisson(10);
#Person ~ Poisson(2);

random Boolean Honest(Person x) ~ Bernoulli(0.9);

origin Person Owner(Login);
#Login(Owner = x) 
  if Honest(x) then = 1 
    else ~ Geometric(0.8);

random Boolean Transaction(Login x, Login y) {
  if x!=y & Owner(x) == Owner(y) then ~ Bernoulli(0.9)
  else if x!=y then 
    ~ TabularCPD({true -> ~Categorical({true -> 0.6, false -> 0.4}), 
                 false -> ~Categorical({true -> 0.1, false -> 0.9})}, 
                Honest(Owner(x)))
};

random Boolean Recommends(Login x, Login y) {
  if (x!=y) & Transaction(x,y) then
    if Owner(x) == Owner(y) then ~ Bernoulli(0.99)
    else ~ TabularCPD(
      {[true, true]   -> ~ Bernoulli(0.9),
       [true, false]  -> ~ Bernoulli(0.1), 
       [false, true]  -> ~ Bernoulli(0.1),
       [false, false] -> ~ Bernoulli(0.1)},
    [Honest(Owner(x)), Honest(Owner(y))])
};

obs {Person p} = {Person1, Person2};

obs {Login l} = {Login1, Login2, Login3};
Query Honest(Person1); 


