/**
 * Sybil attack
 * Typically between 100 and 10,000 real people, about 90% are honest 
 * who has one login ID. Dishonest people can have multiple logins 
 * each. Transactions may occur between logins. If two logins are 
 * owned by the same person, then a transaction is highly likely; 
 * Otherwise, transaction is less likely (depending on honesty 
 * of each login's owner). A login may recommend another after 
 * a transaction: Logins with the same owner usually recommend 
 * each other; Otherwise, probability of recommendation depends 
 * on the honesty of the two owners.
 *
 * 2011-12-21
 */

type Person;
type Login;

origin Person Owner(Login);
random Boolean Honest(Person);
random Boolean Transaction(Login, Login);
random Boolean Recommends(Login, Login);

#Person ~ Poisson[10]();
Honest(x) ~ Bernoulli[0.9]();
#Login(Owner = x) 
    if Honest(x) then = 1 
    else ~ Geometric[0.8]();
   
Transaction(x,y)  
    if x!=y & Owner(x) = Owner(y) then ~Bernoulli[0.9]()
    elseif x!=y then 
        ~TabularCPD[[0.6, 0.4], [0.1, 0.9]](Honest(Owner(x)));

/**
 * another version
Transaction(x,y) ~ 
    if Owner(x) = Owner(y) then Bernoulli[0.8]()
    else TabularCPD[[0.6, 0.4], 
                   [0.6, 0.4], 
                   [0.1, 0.9], 
                   [0.1, 0.9]](Honest(Owner(x)),
                               Honest(Owner(y))); 
 */

Recommends(x,y)  
    if x!=y & Transaction(x,y) & Owner(x) = Owner(y) then 
        ~Bernoulli[0.99]()
    elseif Transaction(x,y) & Owner(x) != Owner(y) then
             ~TabularCPD[[0.9, 0.1], 
                        [0.1, 0.9], 
                        [0.1, 0.9], 
                        [0.1, 0.9]]
                   (Honest(Owner(x)), Honest(Owner(y)));


obs {Person p} = {Person1, Person2, Person3, Person4, Person5, 
                  Person6, Person7, Person8, Person9};

obs {Login l} = {Login1, Login2, Login3, Login4, Login5, Login6,
                Login7, Login8, Login9, Login10, Login11, Login12,
                Login13, Login14, Login15, Login16, Login17, Login18};

/**
obs Honest(Person9) = false;
obs Honest(Person1) = false;
obs Honest(Person2) = true;
obs Honest(Person3) = true;
obs Honest(Person4) = true;
obs Honest(Person5) = true;
obs Honest(Person6) = true;
obs Honest(Person7) = true;
obs Honest(Person8) = true;

obs #Login(Owner = Person9) = 8;
obs #Login(Owner = Person1) = 3;
obs #Login(Owner = Person2) = 1;
obs #Login(Owner = Person3) = 1;
obs #Login(Owner = Person4) = 1;
obs #Login(Owner = Person5) = 1;
obs #Login(Owner = Person6) = 1;
obs #Login(Owner = Person7) = 1;
obs #Login(Owner = Person8) = 1;
*/

obs Transaction(Login7, Login4) = true;
obs Transaction(Login1, Login18) = true;
obs Transaction(Login3, Login13) = false;
obs Transaction(Login15, Login5) = true;
obs Transaction(Login6, Login7) = true;
obs Transaction(Login16, Login8) = true;
obs Transaction(Login11, Login15) = false;
obs Transaction(Login12, Login3) = true;
obs Transaction(Login3, Login1) = true;
obs Transaction(Login3, Login7) = true;
obs Transaction(Login12, Login11) = true;
obs Transaction(Login15, Login11) = true;
obs Transaction(Login9, Login10) = true;
obs Transaction(Login18, Login13) = false;
obs Transaction(Login7, Login9) = false;
obs Transaction(Login9, Login6) = false;
obs Transaction(Login5, Login15) = false;
obs Transaction(Login4, Login17) = false;
obs Transaction(Login8, Login11) = true;
obs Transaction(Login17, Login16) = false;

obs Recommends(Login8, Login16) = false;
obs Recommends(Login8, Login16) = false;
obs Recommends(Login13, Login16) = true;
obs Recommends(Login17, Login8) = false;
obs Recommends(Login17, Login12) = true;
obs Recommends(Login1, Login13) = false;
obs Recommends(Login4, Login6) = true;
obs Recommends(Login1, Login4) = true;
obs Recommends(Login1, Login14) = false;
obs Recommends(Login14, Login13) = true;
obs Recommends(Login8, Login1) = false;
obs Recommends(Login3, Login2) = true;
obs Recommends(Login5, Login6) = true;
obs Recommends(Login17, Login18) = false;
obs Recommends(Login11, Login1) = false;
obs Recommends(Login2, Login10) = false;
obs Recommends(Login16, Login3) = false;
obs Recommends(Login6, Login4) = true;
obs Recommends(Login3, Login5) = true;
obs Recommends(Login11, Login18) = false;


Query Honest(Person9); 


