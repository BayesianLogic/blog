/** Latent Dirichlet Allocation (LDA) represented in blog model.
 * (Blei, Ng and Jordan, 2003)
 * 
 * @author: yiwu
 *   Note: this model has been tested in Swift compiler
 */

type Document;
distinct Document Doc[5];
fixed Integer K = 3;
fixed Integer W = 10;

fixed RealMatrix a = 1.0 * ones(W, 1);
fixed RealMatrix b = 1.0 * ones(K, 1);

random RealMatrix TopicWord(Integer p) ~ Dirichlet(a);
random RealMatrix DocTopic(Document d) ~ Dirichlet(b);
random Integer WordTopic(Document d, Integer p) 
  ~ Discrete(DocTopic(d));
random Integer DocWord(Document d, Integer p)
  ~ Discrete(TopicWord(WordTopic(d, p)));

query DocTopic(Doc[0])[0];
query DocTopic(Doc[0])[1];
query DocTopic(Doc[0])[2];

///////// data ///////////
/*
Doc[0]: cat, kitty, girl, {0, 1, 4}
Doc[1]: dog, doggy, boy,  {2, 3, 5}
Doc[2]: girl, kitty, dress  {4, 1, 9}
Doc[3]: boy, soccer, run  {5, 6, 7}
Doc[4]: soccer, run, player  {6, 7, 8}
Dictionary: cat, kitty, dog, doggy, girl, boy, soccer, run, player, dress
             0   1      2    3      4     5    6        7     8      9
*/

obs DocWord(Doc[0], 0) = 0;
obs DocWord(Doc[0], 1) = 1;
obs DocWord(Doc[0], 2) = 4;
obs DocWord(Doc[1], 0) = 2;
obs DocWord(Doc[1], 1) = 3;
obs DocWord(Doc[1], 2) = 5;
obs DocWord(Doc[2], 0) = 4;
obs DocWord(Doc[2], 1) = 1;
obs DocWord(Doc[2], 2) = 9;
obs DocWord(Doc[3], 0) = 5;
obs DocWord(Doc[3], 1) = 6;
obs DocWord(Doc[3], 2) = 7;
obs DocWord(Doc[4], 0) = 6;
obs DocWord(Doc[4], 1) = 7;
obs DocWord(Doc[4], 2) = 8;
